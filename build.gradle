plugins {
    id 'java'
}

group 'com.cerrealic'
version 'b0.4.0-SNAPSHOT'
description 'Simple but effective Spigot plugin that adds a /buy command.'

sourceCompatibility = 1.8

jar {
	manifest {
		attributes('Main-Class': 'com.cerrealic.ezbuy.EzBuy')
	}

	from {
		configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
	}
}

repositories {
	jcenter()
	mavenCentral()
	maven {
		url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'

		// As of Gradle 5.1, you can limit this to only those
		// dependencies you expect from it
		content {
			//includeGroup 'org.bukkit'
			includeGroup 'org.spigotmc'
		}
	}

	/*
	 As Spigot-API depends on the Bungeecord ChatComponent-API,
	we need to add the Sonatype OSS repository, as Gradle,
	in comparison to maven, doesn't want to understand the ~/.m2
	directory unless added using mavenLocal(). Maven usually just gets
	it from there, as most people have run the BuildTools at least once.
	This is therefore not needed if you're using the full Spigot/CraftBukkit,
	or if you're using the Bukkit API.
	*/
	maven {
		url = 'https://oss.sonatype.org/content/repositories/snapshots/'
	}

	maven {
		url = 'https://ci.ender.zone/plugin/repository/everything/'
	}

	maven {
		name = 'papermc'
		url = 'https://papermc.io/repo/repository/maven-public/'
	}

	maven {
		url = 'https://jitpack.io'
	}
}

dependencies {
	compileOnly 'org.spigotmc:spigot-api:1.15.2-R0.1-SNAPSHOT' // The Spigot API with no shadowing. Requires the OSS repo.
	compileOnly 'org.spigotmc:plugin-annotations:1.2.3-SNAPSHOT'
	annotationProcessor 'org.spigotmc:plugin-annotations:1.2.3-SNAPSHOT'
	compile 'com.github.squiig:cerspi-lib:3.3'

	compileOnly('com.github.MilkBowl:VaultAPI') {
		version {
			require '1.7'
		}
	}

	compileOnly('net.ess3:EssentialsX') {
		version {
			require '2.17.0'
		}
	}
}

configurations.compile {
	resolutionStrategy.cacheChangingModulesFor 30, 'seconds'
}
